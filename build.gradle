// ~~~ GLOBAL ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

allprojects {

    group = 'com.github.schnitker.logmanager'
    
    repositories { 
        mavenCentral()
    }
}

defaultTasks 'assemble'

task wrapper(type: Wrapper) {
    description = 'Defines the default gradle version and installs wrapper'
    gradleVersion = '2.0'
}

// ~~~ Modules ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

subprojects { subproject ->

	apply plugin: 'java'
	apply from:   "${rootProject.projectDir}/gradle/eclipse-project-utf8.gradle"
	apply from:   "${rootProject.projectDir}/gradle/eclipse-project-libsort.gradle"
	apply from:   "${rootProject.projectDir}/gradle/publish-maven.gradle"
	
    // version = rootProject.version
    version = '1.0.1'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    compileJava.options.encoding = 'utf-8'

    // generic dependencies             
    dependencies {
        testCompile 'junit:junit:4.11' 
    }

    jar {
        manifest.attributes["Implementation-Vendor"] = 'Schnitker.com'
        manifest.attributes["Implementation-Title"] = subproject.name
        manifest.attributes["Implementation-Version"] = subproject.version
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
	task javadocJar(type: Jar) {
	    classifier = 'javadoc'
	    from javadoc
	}

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }
       
    artifacts {
        archives sourcesJar, javadocJar
    }
}

// ~~~ Root ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

def getEclipseFilter() {
	return '1.0-projectRelativePath-matches-true-true-(logmgr-.*)'
}

configure(rootProject) {
	apply from: "gradle/eclipse-project-filter.gradle"
}
