apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'

uploadArchives.enabled = false

eclipse {
    wtp {
        facet {
            facet name: 'jst.web', version: '3.0'
            facet name: 'java', version: '1.7'
        }
        component { // fix dependent project with jar file
            file {
                withXml {
                    it.asNode()'wb-module'[0].children()
                        .findAll { it.name() == 'dependent-module' }
                        .findAll { it.attribute('handle').startsWith 'module:/resource' }
                        .each {
                            def modname = it.attribute('handle').tokenize('/').last()
                            it.attributes().put('handle', "module:/classpath/lib/" 
                                                           + project(":$modname").jar.archivePath.toString().replace('\\', '/') )
                         }
                }
            }
        } 
    }

    classpath { // fix tomcat entry
        file {
            withXml {
                xml -> xml.asNode().classpathentry.findAll{
                    it.@path == 'org.eclipse.jst.j2ee.internal.web.container'
                }.each {
                    it.parent().remove(it)
                }
				def attrs = [kind : 'con', path : 'org.eclipse.jst.server.core.container/org.eclipse.jst.server.tomcat.runtimeTarget/Apache Tomcat v7.0']
				new Node(xml.asNode(), 'classpathentry', attrs)

            }
        }
    }
}
